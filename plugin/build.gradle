apply plugin: 'org.jetbrains.intellij'
apply plugin: "idea"
apply plugin: "kotlin"

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compileOnly "org.jetbrains.kotlin:kotlin-runtime:$kotlinVersion"
    compile project(':tooling')
}

def profiles = [
    'idea2016': { intellij ->
        intellij.version = '2016.1'
        intellij.plugins = ['org.jetbrains.kotlin:1.0.4-release-IJ2016.1-112']
//        intellij.plugins = ['org.jetbrains.kotlin:1.1.0-beta-17-IJ2016.1-1@EAP-1.1']
    },
    'idea15': { intellij ->
        intellij.version = '143.1821.5'
        intellij.plugins = ['org.jetbrains.kotlin:1.0.4-release-IJ143-115']
    },
    'local': { intellij ->
        intellij.version = '2016.1'
        intellij.plugins = ['org.jetbrains.kotlin:1.0.4-release-IJ2016.1-112']
        intellij.alternativeIdePath = '/opt/idea'
    }
]

String profile = findProperty("profile") ?: profiles.keySet().first()

intellij {
    profiles[profile].call(delegate)
    pluginName = 'spek-idea-plugin'
    updateSinceUntilBuild = false
    downloadSources = true
}
